[[business:Default]]
[role=group,includesConcepts="business:Subdomain,business:SubdomainCrossfunctional,business:SubdomainDependency,business:TechnicalAspects,business:TechnicalAspectsUnknown,business:TechnicalAspectsDependency"]
== Business

This section describes the application from the business' perspective.

=== Concepts

==== Subdomains
The application consists of several business sub domains that can be identified by naming conventions.

[[business:Subdomain]]
.Creates predefined Subdomain nodes and connects them to all Type nodes via naming conventions.
[source,cypher,role=concept]
----
UNWIND [
    { name: "Account" },
	{ name: "Authentifcation" },
    { name: "Confirmation" },
    { name: "Consents" }
]
AS properties
MERGE (s:Business:Subdomain{name:properties.name})
WITH s
    MATCH (t:Type)<-[:CONTAINS*]-(:Project)
        WHERE t.name CONTAINS s.name
    MERGE (t)-[:BELONGS_TO]->(s)
RETURN s.name as Subdomain, COUNT(t) as Types
----

==== Crossfunctional Aspects
Some types are not specific to a subdomain, but are there for crossfunctional aspects.
[[business:SubdomainCrossfunctional]]
.Mark all types for the current Project that are crossfunctional.
[source,cypher,role=concept,requiresConcepts="business:Subdomain"]
----
MATCH (t:Type)<-[:CONTAINS*]-(:Project)
WHERE NOT (t)-[:BELONGS_TO]->(:Business:Subdomain)
MERGE (s:Business:Subdomain {name: "crossfunctional"})
MERGE (s)<-[:BELONGS_TO]-(t)
RETURN s.name as CrossfunctionalSubdomain, count(t) as Types
----

==== Crossfunctional Aspects

There are dependencies between business sub domains.

[[business:SubdomainDependency]]
.Creates DEPENDS_ON relations between two subdomains if there are Java type dependencies between them.
[source,cypher,role=concept,requiresConcepts="business:SubdomainCrossfunctional"]
----
MATCH
  (t1:Type)-[:BELONGS_TO]->(s1:Business:Subdomain),
  (t2:Type)-[:BELONGS_TO]->(s2:Business:Subdomain),
  (t1)-[d:DEPENDS_ON]->(t2)
WHERE
  s1 <> s2
WITH
  s1, s2, count(d) as weight
MERGE
  (s1)-[d:DEPENDS_ON]->(s2)
SET
  d.weight = weight
RETURN
  s1.name as Business, collect(s2.name) as Dependencies
ORDER BY
  Business
----



==== Technical Aspects
The application consists of several business sub domains that can be identified by naming conventions.

[[business:TechnicalAspects]]
.Creates information about technical aspects using package naming conventions.
[source,cypher,role=concept]
----
UNWIND [
    { name: "Response" },
	{ name: "Model" },
    { name: "Client" },
    { name: "Request" },
	{ name: "Data" },
	{ name: "Manager" },
	{ name: "Helper" },
	{ name: "Controller" }
]
AS properties
MERGE (ta:Business:TechnicalAspect{name:properties.name})
WITH ta
    MATCH (t:Type)<-[:CONTAINS*]-(:Project)
        WHERE t.name CONTAINS ta.name
    MERGE (t)-[:IS_A]->(ta)
RETURN ta.name as TechnicalAspect, COUNT(t) as Types
----

==== Unknown technical aspects
Some technical aspects aren't defined yet, so we mark them accordingly.
[[business:TechnicalAspectsUnknown]]
.Mark all technical aspects that don't belong to an existing technical aspect.
[source,cypher,role=concept,requiresConcepts="business:TechnicalAspects"]
----
MATCH (t:Type)<-[:CONTAINS*]-(:Project)
WHERE NOT (t)-[:IS_A]->(:Business:TechnicalAspect)
MERGE (ta:Business:TechnicalAspect {name: "unknown"})
MERGE (ta)<-[:IS_A]-(t)
RETURN ta.name as UnknownTechnicalAspect, count(t) as Types
----

==== Dependencies between technical aspects

There are dependencies between business technical aspects.

[[business:TechnicalAspectsDependency]]
.Creates DEPENDS_ON relations between two subdomains if there are Java type dependencies between them.
[source,cypher,role=concept,requiresConcepts="business:TechnicalAspectsUnknown"]
----
MATCH
  (t1:Type)-[:IS_A]->(ta1:Business:TechnicalAspect),
  (t2:Type)-[:IS_A]->(ta2:Business:TechnicalAspect),
  (t1)-[d:DEPENDS_ON]->(t2)
WHERE
  ta1 <> ta2
WITH
  ta1, ta2, count(d) as weight
MERGE
  (ta1)-[d:DEPENDS_ON]->(ta2)
SET
  d.weight = weight
RETURN
  ta1.name as TechnicalAspect, collect(ta2.name) as Dependencies
ORDER BY
  TechnicalAspect
----

